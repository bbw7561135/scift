!>
!! @brief
!!
module RandomUtils_
	implicit none
	private
	
	public :: &
		RandomUtils_init, &
		RandomUtils_randomizeVector, &
		RandomUtils_uniform, &
		RandomUtils_randomMultiset, &
		RandomUtils_test
		
	interface RandomUtils_uniform
		module procedure RandomUtils_rUniform
		module procedure RandomUtils_iUniform
	end interface RandomUtils_uniform
		
	interface RandomUtils_randomMultiset
		module procedure RandomUtils_iRandomMultiset
		module procedure RandomUtils_sRandomMultiset
	end interface RandomUtils_randomMultiset
	
	interface
		function prototypeMultisetConstraint( multisetPositions ) result( output )
			integer, allocatable, intent(in) :: multisetPositions(:)
			logical :: output
		end function prototypeMultisetConstraint
	end interface
		
	integer, allocatable :: kMultiCombChosen(:)
	integer(8) :: kMultiCombIterator = -1
	logical :: kMultiCombFirstTime = .true.
	
	logical :: randFirstTime = .true. !<- Used in order to stablish the seed in random procedures
	integer, allocatable :: ids(:)
	procedure(prototypeMultisetConstraint), pointer, private :: MSConstraint !<- This is neccesary only for RandomUtils_randomMultiset
	logical :: randomMultisetLocated = .false.
		
	contains
	
	!>
	!! Initialization of random numbers
	!!
	subroutine RandomUtils_init()
		integer :: i, n, clock
		integer, dimension(:), allocatable :: seed
		real(8) :: rBuffer
		
		if( randFirstTime ) then
			call random_seed(size = n)
			allocate(seed(n))
			call system_clock(count=clock)
			seed = clock + 37 * (/ (i - 1, i = 1, n) /)
			call random_seed(put = seed)
			deallocate(seed)
			
			call random_number( rBuffer ) !<- Desechamos el primer número de la serie, ya que parece que es muy regular respecto al tiempo
			
			randFirstTime = .false.
		end if
	end subroutine RandomUtils_init
	
	!>
	!! Shuffles Vector with Fisher-Yates shuffling
	!!
	subroutine RandomUtils_randomizeVector( arr )
		integer, allocatable, intent(inout) :: arr(:)
		
		integer :: nElems
		integer :: i, s
		integer :: temp
		real(8) :: rBuffer
		
		nElems = size(arr)
		
		if( randFirstTime ) then
			call RandomUtils_init()
			randFirstTime = .false.
		end if
		
		if( nElems > 1 ) then
			i = nElems
			do while( i > 1 )
				call random_number( rBuffer )
				s = int(rBuffer*i)+1
				
				temp = arr(s)
				arr(s) = arr(i)
				arr(i) = temp
				
				i = i-1
			end do
		end if
	end subroutine RandomUtils_randomizeVector
	
	!>
	!! @brief Build a random sample using an uniform distribution
	!! 
	!! @param[out] sample In this array will will store the generated random numbers
	!!                    it should be allocated in advance
	!!
	function RandomUtils_rUniform( range ) result( output )
		real(8), intent(in) :: range(2)
		real(8) :: output
		
		call RandomUtils_init()
		call random_number( output )
		
		output = range(1) + output*abs(range(2)-range(1))
	end function RandomUtils_rUniform
	
	!>
	!! @brief Build a random sample using an uniform distribution
	!! 
	!! @param[out] sample In this array will will store the generated random numbers
	!!                    it should be allocated in advance
	!!
	function RandomUtils_iUniform( range ) result( output )
		integer, intent(in) :: range(2)
		integer :: output
		
		real(8) :: rBuffer
		
		call RandomUtils_init()
		call random_number( rBuffer )
		
		output = int(rBuffer*(range(2)+1-range(1)))+range(1)
	end function RandomUtils_iUniform
	
	!>
	!! @brief Chooses the first one combination with repetitions which
	!!        satisfy the constraint. This implementation uses a
	!!        tree-like algorithm with random browsing.
	!! 
	!! @param[in] nElems Number of elements
	!! @param[in] sGroups size of the groups
	!! @param[out] randomItem random multiset
	!! 
	!! example: if S={0,1,3,2} then the combination of these elements in groups of two elements
	!!          which satisfy the constraint e_i + e_j = 3 are:
	!!     1)   0,3
	!!     2)   2,1
	recursive subroutine randomMultisetBase( nElems, sGroups, randomItem, nChosen, at )
		integer, intent(in) :: nElems
		integer, intent(in) :: sGroups
		integer, allocatable, intent(inout) :: randomItem(:)
		integer, optional, intent(in) :: nChosen
		integer, optional, intent(in) :: at
		
		integer :: effNChosen
		integer :: effAt
		
		integer :: i
		integer :: counter
		
		effNChosen = 1
		if( present(nChosen) ) effNChosen = nChosen
		
		effAt = 1
		if( present(at) ) effAt = at
		
		!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
		! When this point is reached, the current combination
		! is the kMultiCombChosen vector
		if ( effNChosen == sGroups+1 ) then
			randomItem(:) = kMultiCombChosen(:)
			
			kMultiCombIterator = kMultiCombIterator + 1
			randomMultisetLocated = MSConstraint( kMultiCombChosen )
			
			return
		end if
			
		if( .not. randomMultisetLocated ) then
			!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
			! Build all possibilities for each element in the item array
			do i=effAt,nElems
				kMultiCombChosen(effNChosen) = ids(i)
				
				call randomMultisetBase( nElems, sGroups, randomItem, effNChosen + 1, i )
				
				if( randomMultisetLocated ) return
			end do
		end if
		
		return
	end subroutine randomMultisetBase
	
	!>
	!! @brief
	!!
	subroutine RandomUtils_sRandomMultiset( elems, sGroups, randomItem, constrainFunction )
		character(10), allocatable, intent(in) :: elems(:) ! @todo Hay que cambiar character(10) por character(:) allocatable o algo así
		integer, intent(in) :: sGroups
		character(10), allocatable, intent(inout) :: randomItem(:)
		procedure(prototypeMultisetConstraint) :: constrainFunction
		
		integer :: i, nElems
		integer, allocatable :: randomItemBase(:)
		
		nElems = size(elems)
		
		!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
		! It initializes the global parameters
		if( allocated(kMultiCombChosen) ) deallocate( kMultiCombChosen )
		kMultiCombIterator = 1
		
		allocate( kMultiCombChosen(sGroups) )
		kMultiCombChosen = 0
		
		allocate( randomItemBase( sGroups ) )
		randomItemBase = 0
		
		allocate( ids(nElems) )
		
		do i=1,nElems
			ids(i) = i
		end do
		
		call RandomUtils_randomizeVector( ids )
		
		randomMultisetLocated = .false.
		MSConstraint => constrainFunction
		
		!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
		! It searches the random multiset
		call randomMultisetBase( size(elems), sGroups, randomItemBase )
		
		!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
		! It establish the original values for the global parameters
		kMultiCombIterator = -1
		deallocate( kMultiCombChosen )
		deallocate( ids )
		MSConstraint => null()
		
		if( .not. randomMultisetLocated ) then
			write(6,"(A)") "### ERROR ### RandomUtils_randomMultiset: It can't be located a multiset with chosen constraint"
			write(6,"(A)") "                                   Don't forget that the constraint don't must be dependent of order of elements"
			stop
		end if
		
		randomMultisetLocated = .false.
		
		if( allocated(randomItem) ) deallocate(randomItem)
		allocate( randomItem(sGroups) )
		
		do i=1,sGroups
			randomItem(i) = elems( randomItemBase(i) )
		end do
		
		deallocate( randomItemBase )
	end subroutine RandomUtils_sRandomMultiset
	
	!>
	!! @brief
	!!
	subroutine RandomUtils_iRandomMultiset( elems, sGroups, randomItem, constrainFunction )
		integer, allocatable, intent(in) :: elems(:)
		integer, intent(in) :: sGroups
		integer, allocatable, intent(inout) :: randomItem(:)
		procedure(prototypeMultisetConstraint) :: constrainFunction
		
		integer :: i, nElems
		integer, allocatable :: randomItemBase(:)
		
		nElems = size(elems)
		
		!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
		! It initializes the global parameters
		if( allocated(kMultiCombChosen) ) deallocate( kMultiCombChosen )
		kMultiCombIterator = 1
		
		allocate( kMultiCombChosen(sGroups) )
		kMultiCombChosen = 0
		
		allocate( randomItemBase( sGroups ) )
		randomItemBase = 0
		
		allocate( ids(nElems) )
		
		do i=1,nElems
			ids(i) = i
		end do
		
		call RandomUtils_randomizeVector( ids )
		
		randomMultisetLocated = .false.
		MSConstraint => constrainFunction
		
		!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
		! It searches the random multiset
		call randomMultisetBase( size(elems), sGroups, randomItemBase )
		
		!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
		! It establish the original values for the global parameters
		kMultiCombIterator = -1
		deallocate( kMultiCombChosen )
		deallocate( ids )
		MSConstraint => null()
		
		if( .not. randomMultisetLocated ) then
			write(6,"(A)") "### ERROR ### RandomUtils_randomMultiset: It can't be located a multiset with chosen constraint"
			write(6,"(A)") "                                   Don't forget that the constraint don't must be dependent of order of elements"
			stop
		end if
		
		randomMultisetLocated = .false.
		
		if( allocated(randomItem) ) deallocate(randomItem)
		allocate( randomItem(sGroups) )
		
		do i=1,sGroups
			randomItem(i) = elems( randomItemBase(i) )
		end do
		
		deallocate( randomItemBase )
	end subroutine RandomUtils_iRandomMultiset
	
	!>
	!! This is only necessary for RandomUtils_test method
	!!
	function myConstrain( multisetPositions ) result( output )
		integer, allocatable, intent(in) :: multisetPositions(:)
		logical :: output
		
		integer :: i, j
		
		do i=1,size(multisetPositions)
			do j=1,size(multisetPositions)
! 				if( multisetPositions(i) == 1 .and. multisetPositions(j) == 3 ) then ! Que la cadena tenga una "a" y una "c"
! 					output = .true.
! 					return
! 				end if

				if( multisetPositions(i) == 1 .or. multisetPositions(j) == 1 ) then ! Que la cadena tenga una "a" en cualquier posición
					output = .true.
					return
				end if
			end do
		end do

		output = .false.
	end function myConstrain
	
	!>
	!! @brief Test method
	!!
	subroutine RandomUtils_test()
		integer, allocatable :: iArray(:)
		character(10), allocatable :: strArray(:)
		character(10), allocatable :: comb(:)
		
		integer :: i, j
		
		write(*,*) ""
		write(*,*) "Randomize vectors"
		write(*,*) "================="
		
		allocate( iArray(5) )
		
		iArray = [ 1, 2, 3, 4, 5 ]
		write(*,"(A,5I3,A)") "  original vector = [", iArray, " ]"
		call RandomUtils_randomizeVector( iArray )
		write(*,"(A,5I3,A)") "randomized vector = [", iArray, " ]"
		
		deallocate( iArray )
		
		write(*,*) ""
		write(*,*) "Uniform distribution"
		write(*,*) "===================="
		write(*,*) ""
		write(*,*) "RandomUtils_uniform( [1.0_8,7.5_8] )"
		do i=1,10
			write(*,"(I5,F10.5)") i, RandomUtils_uniform( [1.0_8,7.5_8] )
		end do
		write(*,*) ""
		write(*,*) "RandomUtils_uniform( [1,7] )"
		do i=1,10
			write(*,"(I5,I5)") i, RandomUtils_uniform( [1,7] )
		end do
		
		write(*,*) ""
		write(*,*) "Random multiset"
		write(*,*) "==============="

		allocate( strArray(7) )
		strArray = [ "a", "b", "c", "d", "e", "f", "g" ]
		
		write(*,*) "set = [ 'a', 'b', 'c', 'd', 'e', 'f', 'g' ]"
		write(*,*)
		
		call RandomUtils_randomMultiset( strArray, 3, comb, myConstrain )
		write(*,*) ""
		write(*,*) "The constraint is: The multiset must have the letter a in any position"
		write(*,*) "RandomUtils_randomMultiset( nElems, 3 ) = ", ( trim(comb(i)), i=1,3 )
		
		!#######################################################
		! To check using a histogram
		write(*,*) ""
		write(*,"(A)", advance="no") "Generation of 1000000 random multiset with the same constraint ... "
		
		open( 10, file="salida" )
		do i=1,1000000
			call RandomUtils_randomMultiset( strArray, 3, comb, myConstrain )
			write(10,*) ( trim(comb(j)), j=1,3 )
		end do
		close( 10 )
		
		write(*,*) "OK"
		write(*,*) ""
		write(*,*) "You can check it using the following awk command:"
#define cc achar(34)//achar(34)
		write(*,"(A)") "awk 'BEGIN{nData=0}{n=split($1,arr,"//cc//"); asort(arr); s="//cc//"; for(i=1;i<=n;i++){s=s"//cc//"arr[i]}; if(s in map){ map[s] = map[s]+1 }else{ map[s]=1 }; nData+=1}END{ for(k in map) print k, map[k]/nData }' salida"
#undef cc
		
		!#######################################################
		
		deallocate( strArray )
		deallocate( comb )
	end subroutine RandomUtils_test
	
end module RandomUtils_
